# Maintainer: Bambang Purnomosidi D. P. <bpurnomo at akakom dot ac dot id>
# Contributor: Jan Steemann <j.steemann at triagens dot de>
pkgname=arangodb
pkgver=2.1.2
pkgrel=1
pkgdesc="A universal open-source database with a flexible data model for documents, graphs, and key-values."
arch=('i686' 'x86_64')
url="http://www.arangodb.org/"
license=('Apache')
depends=('openssl' 'bash' 'systemd' 'icu')
makedepends=('python2' 'go')
install=arangodb.install
source=("http://www.arangodb.org/repositories/Source/ArangoDB-$pkgver.tar.bz2"
        "arangodb.service")
md5sums=('0414f54b4fc2ecd42a7e46bb10b84f43'
         'f31bc51120192bd4ef36b559f3428135')

build() {
  cd "$srcdir/ArangoDB-$pkgver"
  ln -s -f /usr/bin/python2 python
  export PATH="`pwd`:$PATH"
  #./configure --bindir=/usr/bin --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var --enable-all-in-one-v8 --enable-all-in-one-libev --enable-all-in-one-icu
  make
}

check() {
  cd "$srcdir/ArangoDB-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/ArangoDB-$pkgver"
  make DESTDIR="$pkgdir/" install
  mkdir -p $pkgdir/usr/share/doc/arangodb
  cp -R $srcdir/ArangoDB-$pkgver/Documentation/* $pkgdir/usr/share/doc/arangodb/
  mkdir -p $pkgdir/usr/lib/systemd/system
  # This line:
  # cp $srcdir/ArangoDB-$pkgver/etc/systemd/arangodb.service $pkgdir/usr/lib/systemd/system/
  # changed into:
  cp ../../arangodb.service $pkgdir/usr/lib/systemd/system/
  # see:
  # https://www.archlinux.org/news/binaries-move-to-usrbin-requiring-update-intervention/
  rm -rf $pkgdir/usr/share/doc/arangodb/man*
}

# vim:set ts=2 sw=2 et:
